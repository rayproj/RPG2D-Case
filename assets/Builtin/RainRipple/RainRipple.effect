// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - passes:
    - vert: sprite-vs:vert
      frag: sprite-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        alphaThreshold: { value: 0.5 }
        _RainRippleTex: { value: white }
        _RainRippleTiling: { value: [1.0, 1.0], target: _RainRippleParams.xy }
        _RainRippleSpeed: { value: 1.0 , target: _RainRippleParams.z }
        _RainRippleIntensity: { value: 1.0 , target: _RainRippleParams.w }
}%

CCProgram sprite-vs %{
  precision highp float;
  #include <builtin/uniforms/cc-global>
  #if USE_LOCAL
    #include <builtin/uniforms/cc-local>
  #endif
  #if SAMPLE_FROM_RT
    #include <common/common-define>
  #endif
  in vec3 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;
  in float a_texIdx;
  out float texIdx;
  out vec4 color;
  out vec2 uv0;
  out vec3 v_worldPos;

  vec4 vert () {
    vec4 pos = vec4(a_position, 1);

    #if USE_LOCAL
      pos = cc_matWorld * pos;
    #endif

    v_worldPos = pos.xyz;

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    uv0 = a_texCoord;
    #if SAMPLE_FROM_RT
      CC_HANDLE_RT_SAMPLE_FLIP(uv0);
    #endif
    color = a_color;
    texIdx = a_texIdx;
    return pos;
  }
}%

CCProgram sprite-fs %{
  precision highp float;
  #include <builtin/internal/embedded-alpha>
  #include <builtin/internal/alpha-test>
  #include <builtin/uniforms/cc-global>

  in vec4 color;
  in vec3 v_worldPos;

  #if USE_TEXTURE
    in vec2 uv0;
    in float texIdx;

    #pragma builtin(local)
    layout(set = 2, binding = 12) uniform sampler2D cc_spriteTexture;
  #endif

  uniform sampler2D _RainRippleTex;
  uniform Constants {
    vec4 _RainRippleParams;
  };

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      o *= CCSampleWithAlphaSeparated(cc_spriteTexture, uv0); 
      
      #if IS_GRAY
        float gray  = 0.2126 * o.r + 0.7152 * o.g + 0.0722 * o.b;
        o.r = o.g = o.b = gray;
      #endif
    #endif

    o *= color;

    vec2 uv = v_worldPos.xy * _RainRippleParams.xy;
    float t1 = fract(cc_time.x * _RainRippleParams.z);
    float t2 = fract(cc_time.x * _RainRippleParams.z + 0.5);
    vec4 rippleCol1 = texture(_RainRippleTex, uv);
    vec4 rippleCol2 = texture(_RainRippleTex, uv + 0.5);
    float col1 = 1.0 - distance(t1 - 1.0 + rippleCol1.r, 0.05) / 0.05;
    col1 = clamp(col1, 0.0, 1.0);
    float col2 = 1.0 - distance(t2 - 1.0 + rippleCol2.r, 0.05) / 0.05;
    col2 = clamp(col2, 0.0, 1.0);
    float final = mix(col1, col2, abs(0.5 - t1) * 2.0);
    o.rgb += vec3(final, final, final) * _RainRippleParams.w;

    ALPHA_TEST(o);
    return o;
  }
}%
